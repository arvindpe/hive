A. create database
------------------
create database retail;

--this creates a folder by the name of retail.db under /user/hive/warehouse

A1. show all the databases in hive
----------------------------------
show databases;

B. Select a database
--------------------
use retail;


C1. Create transaction table
-------------------------------
create table txnrecords(txnno INT, txndate STRING, custno INT, amount DOUBLE, 
category STRING, product STRING, city STRING, state STRING, spendby STRING)
row format delimited
fields terminated by ','
stored as textfile;

C2. Create customer table
-------------------------------
create table customer(custno INT, firstname STRING, lastname STRING, age INT, profession STRING)
row format delimited
fields terminated by ','
stored as textfile;
 
D1. Load the data into the table (from local file system)
-----------------------------------------------------
LOAD DATA LOCAL INPATH '/home/hduser/txns1.txt' OVERWRITE INTO TABLE txnrecords;
LOAD DATA LOCAL INPATH '/home/hduser/custs' OVERWRITE INTO TABLE customer;

D2. Load the data into the table (from hdfs system)
-----------------------------------------------------
LOAD DATA INPATH '/<hdfs path>/txns1.txt' OVERWRITE INTO TABLE txnrecords;
LOAD DATA INPATH '/custs' OVERWRITE INTO TABLE customer;

trunctate table customer;
hadoop fs -put custs /niit
LOAD DATA INPATH '/niit/custs' OVERWRITE INTO TABLE customer;
 
E 1. Describing metadata or schema of the table
---------------------------------------------
describe txnrecords;

E 2. Describing detailed metadata or schema of the table
---------------------------------------------
describe extended txnrecords;

F. Counting no of records
-------------------------
select count(*) from txnrecords;

G1. Count of each profession in the Customers List
---------------------------------------------------
select profession, count(profession) as headcount from customer group by profession order by profession;

G2. Top 10 Customers List
------------------------
select a.custno, b.firstname,b.lastname, b.age, b.profession, sum(a.amount) as amt from txnrecords a, customer b where a.custno=b.custno group by a.custno, b.firstname, b.lastname, b.age, b.profession order by amt desc limit 10;

J.create external tables
----------------------
***first load the data set on hadoop

$ hadoop fs -mkdir /user/training
$ hadoop fs -put /home/hduser/custs /user/training

create external table customer(custno string, firstname string, lastname string, age int,profession string)
row format delimited
fields terminated by ','
stored as textfile
location '/user/training';

select * from customer;

describe extended customer;


K 1. Inserting output in local file
------------------------------
INSERT OVERWRITE LOCAL DIRECTORY '/home/hduser/niit/custcount' row format delimited fields terminated by ',' 
select profession, count(*) as headcount from customer group by profession order by headcount desc limit 10;


INSERT OVERWRITE LOCAL DIRECTORY '/home/hduser/niit/topten' row format delimited fields terminated by ',' 
select a.custno, b.firstname,b.lastname, b.age, b.profession, round(sum(a.amount),2) as amt from txnrecords a, customer b where a.custno=b.custno group by a.custno, b.firstname, b.lastname, b.age, b.profession order by amt desc limit 10;


K 2. Inserting output in hdfs file system
-------------------------------------------
INSERT OVERWRITE DIRECTORY '/niit/custcount' row format delimited fields terminated by ',' 
select profession, count(*) from customer group by profession;


INSERT OVERWRITE DIRECTORY '/niit/topten' row format delimited fields terminated by ',' 
select a.custno, b.firstname,b.lastname, b.age, b.profession, sum(a.amount) as amt from txnrecords a, customer b where a.custno=b.custno group by a.custno, b.firstname, b.lastname, b.age, b.profession order by amt desc limit 10;

L. to execute script from command prompt
--------------------------------------
$ hive -f filename.sql
$ hive -f professioncount.sql

M. to execute command from command prompt
--------------------------------------
$ hive -e "select * from retail.customer"


N1. how do i know i am in which database currently
--------------------------------------------------
set hive.cli.print.current.db=true;

N2. how do i print my headers of my table
-------------------------------------
set hive.cli.print.header=true;

N3. how do i set my default file format
---------------------------------------
set hive.default.fileformat=textfile;

O.run hive query from linux terminal and copy the result to your own file
-----------------------------------------------------------------------
$ hive -e "select * from retail.customer" > /home/hduser/oracle_output.txt

Q. inserting output into another table ( make sure results table is created beforehand)
---------------------------------------------------------------------------------------
create table Airsports(txnno INT, txndate STRING, custno INT, amount DOUBLE, 
category STRING, product STRING, city STRING, state STRING, spendby STRING)
row format delimited
fields terminated by ','
stored as textfile;

insert overwrite table Airsports select * from txnrecords where category = 'Air Sports';

R1. find sales done in each mode and their percentage
-----------------------------------------------------
create table totalsales (total bigint)
row format delimited                                                                                  
fields terminated by ',';   

insert overwrite table totalsales                                                                           
select sum(amount) from txnrecords;

select a.spendby, round(sum(a.amount),2) as typesales, round((sum(a.amount)/total*100),2) as salespercent from txnrecords a, totalsales b group by a.spendby, b.total ;



R2.find sales based on age group
--------------------------------

create table out1 (custno int,firstname string,age int,profession string,amount double,product string)
row format delimited                                                                                  
fields terminated by ',';   


insert overwrite table out1                                                                           
select a.custno,a.firstname,a.age,a.profession,b.amount,b.product                                     
from customer a JOIN txnrecords b ON a.custno = b.custno;     

select * from out1 limit 100;

create table out2 (custno int,firstname string,age int,profession string,amount double,product string, level string)
row format delimited                                                                                  
fields terminated by ',';   

insert overwrite table out2
select * , case when age<30 then 'low' when age>=30 and age < 50 then 'middle' when age>=50 then 'old' 
else 'others' end
from out1;


select * from out2 limit 100; 

describe out2;  

create table out3 (level string, amount double)                                                                                   
row format delimited
fields terminated by ',';

insert overwrite table out3  
select level,sum(amount) from out2 group by level;

select * from out3;


